! -*- coding: utf-8 -*-
! University of Zurich - Institute of Computational Linguistics
! Morphology anlysis for Romansh: Irregular Verbs
! Author: Reto Baumgartner
! Date: June 2013

! These verbs are listed in Caduff&Caprez in article 128

Multichar_Symbols +Verb +PresInd +ImpInd +Cond +Con +Impv +Inf +Gerund +1P +2P +3P +Sg +Pl
*DOUB +Pron +Pers +Masc +Fem +MF +Nom ^| +Apo

LEXICON Root
AVAIR ;
COIER ;
CRAIR ;
CUAR ;
DAR ;
DASTGAR ;
DIR ;
DUAIR ;
ESSER ;
FAR ;
FUGIR ;
IR ;
NUAR ;
PRUIR ;
PUDAIR ;
RUIER ;
SAVAIR ;
STAR ;
STUAIR ;
SURRIR ;
TERRENISAR ;
VAIR ;
VEGNIR ;
VULAIR ;
! Prefigated irregular verbs
CUNTRAPREFIX ;
DISPREFIX ;
PREPREFIX ;
REPREFIX ;
SPREFIX ;
STRAPREFIX ;
SUTTAPREFIX ;

LEXICON AVAIR
avair+Verb+PresInd+1P+Sg:hai	PronSg1 ;
avair+Verb+PresInd+2P+Sg:has	# ;
avair+Verb+PresInd+3P+Sg:ha		PronSg3Short ;
avair+Verb+PresInd+1P+Pl:avain	PronPl1 ;
avair+Verb+PresInd+2P+Pl:avais	# ;
avair+Verb+PresInd+3P+Pl:han	PronPl3 ;
avair:ave					ImpIndEnd ;
avair:haj					ConjEnd ;
avair:ave					CondEnd ;
avair:ave					GerundEnd ;
avair						InfEnd ;
avair+Verb+Impv+2P+Sg:hajas	# ;
avair+Verb+Impv+2P+Pl:hajas	# ;


LEXICON COIER
coier+Verb+PresInd+1P+Sg:coi	PronSg1 ;
coier+Verb+PresInd+2P+Sg:cois	# ;
coier+Verb+PresInd+3P+Sg:coi	PronSg3Short ;
coier+Verb+PresInd+1P+Pl:cuiain	PronPl1 ;
coier+Verb+PresInd+2P+Pl:cuiais	# ;
coier+Verb+PresInd+3P+Pl:coin	PronPl3 ;
coier:cuie					ImpIndEnd ;
coier:coj					ConjEnd ;
coier:cuie					CondEnd ;
coier:cuie					GerundEnd ;
coier						InfEnd ;
coier+Verb+Impv+2P+Sg:coia	# ;
coier+Verb+Impv+2P+Pl:cuiai	# ;


LEXICON CRAIR
crair+Verb+PresInd+1P+Sg:crai	PronSg1 ;
crair+Verb+PresInd+2P+Sg:crais	# ;
crair+Verb+PresInd+3P+Sg:crai		PronSg3Short ;
crair+Verb+PresInd+1P+Pl:crajain	PronPl1 ;
crair+Verb+PresInd+1P+Pl:cartain	PronPl1 ;
crair+Verb+PresInd+2P+Pl:crajais	# ;
crair+Verb+PresInd+2P+Pl:cartais	# ;
crair+Verb+PresInd+3P+Pl:crain	PronPl3 ;
crair:craje					ImpIndEnd ;
crair:carte					ImpIndEnd ;
crair:craj					ConjEnd ;
crair:craje					CondEnd ;
crair:carte					CondEnd ;
crair:craje					GerundEnd ;
crair:carte					GerundEnd ;
crair						InfEnd ;
crair+Verb+Impv+2P+Sg:crai	# ;
crair+Verb+Impv+2P+Pl:crajai	# ;


LEXICON CUAR
cuar+Verb+PresInd+1P+Sg:cuel	PronSg1 ;
cuar+Verb+PresInd+2P+Sg:cuas	# ;
cuar+Verb+PresInd+3P+Sg:cua		PronSg3Short ;
cuar+Verb+PresInd+1P+Pl:cuain	PronPl1 ;
cuar+Verb+PresInd+2P+Pl:cuais	# ;
cuar+Verb+PresInd+3P+Pl:cuan	PronPl3 ;
cuar:cua					ImpIndEnd ;
cuar:cuvi					ConjEnd ;
cuar:cua					CondEnd ;
cuar:cuo					GerundEnd ;
cuar						InfEnd ;
cuar+Verb+Impv+2P+Sg:cua	# ;
cuar+Verb+Impv+2P+Pl:cuai	# ;


LEXICON DAR
dar+Verb+PresInd+1P+Sg:dun		PronSg1 ;
dar+Verb+PresInd+2P+Sg:das		# ;
dar+Verb+PresInd+3P+Sg:datt		PronSg3Cons ;
dar+Verb+PresInd+1P+Pl:dain		PronPl1 ;
dar+Verb+PresInd+2P+Pl:dais		# ;
dar+Verb+PresInd+3P+Pl:dattan	PronPl3 ;
dar:de						ImpIndEnd ;
dar:detti					ConjEnd ;
dar:de						CondEnd ;
dar:do						GerundEnd ;
dar							InfEnd ;
dar+Verb+Impv+2P+Sg:dà		# ;
dar+Verb+Impv+2P+Pl:dai		# ;


LEXICON DASTGAR
dastgar+Verb+PresInd+1P+Sg:dastg	PronSg1 ;
dastgar+Verb+PresInd+2P+Sg:dastgas	# ;
dastgar+Verb+PresInd+3P+Sg:dastg	PronSg3 ; ! Only stem! Endings to be added.
dastgar+Verb+PresInd+1P+Pl:dastgain	PronPl1 ;
dastgar+Verb+PresInd+2P+Pl:dastgais	# ;
dastgar+Verb+PresInd+3P+Pl:dastgan	PronPl3 ;
dastgar:dastga					ImpIndEnd ;
dastgar:dastgi					ConjEnd ;
dastgar:dastga					CondEnd ;
dastgar:dastgo					GerundEnd ;
dastgar							InfEnd ;
dastgar+Verb+Impv+2P+Sg:dastga	# ;
dastgar+Verb+Impv+2P+Pl:dastgai	# ;


LEXICON DIR
dir+Verb+PresInd+1P+Sg:di		PronSg1 ;
dir+Verb+PresInd+2P+Sg:dis		# ;
dir+Verb+PresInd+3P+Sg:di		PronSg3Short ;
dir+Verb+PresInd+1P+Pl:schain	PronPl1 ;
dir+Verb+PresInd+2P+Pl:schais	# ;
dir+Verb+PresInd+3P+Pl:din		PronPl3 ;
dir:sche					ImpIndEnd ;
dir:di						ConjEnd ;
dir:sche					CondEnd ;
dir:sche					GerundEnd ;
dir							InfEnd ;
dir+Verb+Impv+2P+Sg:di		# ;
dir+Verb+Impv+2P+Pl:schai	# ;


LEXICON DUAIR
duair+Verb+PresInd+1P+Sg:duai	PronSg1 ;
duair+Verb+PresInd+2P+Sg:duais	# ;
duair+Verb+PresInd+3P+Sg:duai	PronSg3Short ;
duair+Verb+PresInd+1P+Pl:duain	PronPl1 ;
duair+Verb+PresInd+2P+Pl:duais	# ;
duair+Verb+PresInd+3P+Pl:duain	PronPl3 ;
duair:due					ImpIndEnd ;
duair:duaj					ConjEnd ;
duair:die					CondEnd ;
duair:due					GerundEnd ;
duair						InfEnd ;
duair+Verb+Impv+2P+Sg:dua	# ;
duair+Verb+Impv+2P+Pl:duai	# ;


LEXICON ESSER
esser+Verb+PresInd+1P+Sg:sun	PronSg1 ;
esser+Verb+PresInd+2P+Sg:es		# ;
esser+Verb+PresInd+3P+Sg:è		# ;
esser+Verb+PresInd+1P+Pl:essan	PronPl1 ;
esser+Verb+PresInd+2P+Pl:essas	# ;
esser+Verb+PresInd+3P+Pl:èn		PronPl3 ;
esser+Verb+ImpInd+1P+Sg:era		# ;
esser+Verb+ImpInd+2P+Sg:eras	# ;
esser+Verb+ImpInd+3P+Sg:er		PronSg3 ; ! Only stem. Endings to be added.
esser+Verb+ImpInd+1P+Pl:eran	PronPl1 ;
esser+Verb+ImpInd+2P+Pl:eras	# ;
esser+Verb+ImpInd+3P+Pl:eran	PronPl3 ;
esser:saj					ConjEnd ;
esser:fi					CondEnd ;
esser:esse					GerundEnd ;
esser:sio					GerundEnd ;
esser						InfEnd ;
esser+Verb+Impv+2P+Sg:sajas	# ;
esser+Verb+Impv+2P+Pl:sajas	# ;
! Suffigated Forms:
esser+Verb+PresInd+3P+Sg^|+Pron+Pers+Nom+3P+Masc+Sg:è'l	#;
esser+Verb+PresInd+3P+Sg^|+Pron+Pers+Nom+3P+Fem+Sg:è'la	#;
esser+Verb+PresInd+3P+Sg^|+Pron+Pers+Nom+3P+MF+Sg:èsi	#;
esser+Verb+PresInd+3P+Sg+Apo:èn	#;


LEXICON FAR
far+Verb+PresInd+1P+Sg:fatsch	PronSg1 ;
far+Verb+PresInd+2P+Sg:fas		# ;
far+Verb+PresInd+3P+Sg:fa		PronSg3Short ;
far+Verb+PresInd+1P+Pl:faschain	PronPl1 ;
far+Verb+PresInd+2P+Pl:faschais	# ;
far+Verb+PresInd+3P+Pl:fan		PronPl3 ;
far:fasche					ImpIndEnd ;
far:fetschi					ConjEnd ;
far:fasche					CondEnd ;
far:fascho					GerundEnd ;
far							InfEnd ;
far+Verb+Impv+2P+Sg:fa		# ;
far+Verb+Impv+2P+Pl:faschai	# ;


LEXICON FUGIR
fugir+Verb+PresInd+1P+Sg:fugel	PronSg1 ;
fugir+Verb+PresInd+2P+Sg:fuis	# ;
fugir+Verb+PresInd+3P+Sg:fui	PronSg3Short ;
fugir+Verb+PresInd+1P+Pl:fugin	PronPl1 ;
fugir+Verb+PresInd+2P+Pl:fugis	# ;
fugir+Verb+PresInd+3P+Pl:fuin	PronPl3 ;
fugir:fugi					ImpIndEnd ;
fugir:fugi					ConjEnd ;
fugir:fugi					CondEnd ;
fugir:fugi					GerundEnd ;
fugir						InfEnd ;
fugir+Verb+Impv+2P+Sg:fui	# ;
fugir+Verb+Impv+2P+Pl:fugi	# ;


LEXICON IR
ir+Verb+PresInd+1P+Sg:vom	PronSg1 ;
ir+Verb+PresInd+2P+Sg:vas	# ;
ir+Verb+PresInd+3P+Sg:va	PronSg3Short ;
ir+Verb+PresInd+1P+Pl:giain	PronPl1 ;
ir+Verb+PresInd+2P+Pl:giais	# ;
ir+Verb+PresInd+3P+Pl:van	PronPl3 ;
ir:gie					ImpIndEnd ;
ir:giaj					ConjEnd ;
ir:gie					CondEnd ;
ir:gio					GerundEnd ;
ir						InfEnd ;
ir+Verb+Impv+2P+Sg:va	# ;
ir+Verb+Impv+2P+Pl:giai	# ;


LEXICON NUAR
nuar+Verb+PresInd+1P+Sg:nuel	PronSg1 ;
nuar+Verb+PresInd+2P+Sg:nuas	# ;
nuar+Verb+PresInd+3P+Sg:nua		PronSg3Short ;
nuar+Verb+PresInd+1P+Pl:nuain	PronPl1 ;
nuar+Verb+PresInd+2P+Pl:nuais	# ;
nuar+Verb+PresInd+3P+Pl:nuan	PronPl3 ;
nuar:nua					ImpIndEnd ;
nuar:nuvi					ConjEnd ;
nuar:nua					CondEnd ;
nuar:nuo					GerundEnd ;
nuar						InfEnd ;
nuar+Verb+Impv+2P+Sg:nua	# ;
nuar+Verb+Impv+2P+Pl:nuai	# ;


LEXICON PRUIR
pruir+Verb+PresInd+1P+Sg:prui	PronSg1 ;
pruir+Verb+PresInd+2P+Sg:pruis	# ;
pruir+Verb+PresInd+3P+Sg:prui	PronSg3Short ;
pruir+Verb+PresInd+1P+Pl:pruin	PronPl1 ;
pruir+Verb+PresInd+2P+Pl:pruis	# ;
pruir+Verb+PresInd+3P+Pl:pruin	PronPl3 ;
pruir:prui					ImpIndEnd ;
pruir:pruj					ConjEnd ;
pruir:prui					CondEnd ;
pruir:prui					GerundEnd ;
pruir						InfEnd ;
pruir+Verb+Impv+2P+Sg:prúi	# ;
pruir+Verb+Impv+2P+Pl:pruí	# ;


LEXICON PUDAIR
pudair+Verb+PresInd+1P+Sg:poss		PronSg1 ;
pudair+Verb+PresInd+2P+Sg:pos		# ;
pudair+Verb+PresInd+3P+Sg:po		PronSg3Short ;
pudair+Verb+PresInd+1P+Pl:pudain	PronPl1 ;
pudair+Verb+PresInd+2P+Pl:pudais	# ;
pudair+Verb+PresInd+3P+Pl:pon		PronPl3 ;
pudair:pude						ImpIndEnd ;
pudair:possi					ConjEnd ;
pudair:pude						CondEnd ;
pudair:pude						GerundEnd ;
pudair							InfEnd ;


LEXICON RUIER
ruier+Verb+PresInd+1P+Sg:rui	PronSg1 ;
ruier+Verb+PresInd+2P+Sg:ruis	# ;
ruier+Verb+PresInd+3P+Sg:rui	PronSg3Short ;
ruier+Verb+PresInd+1P+Pl:ruin	PronPl1 ;
ruier+Verb+PresInd+2P+Pl:ruis	# ;
ruier+Verb+PresInd+3P+Pl:ruin	PronPl3 ;
ruier:ruie					ImpIndEnd ;
ruier:ruj					ConjEnd ;
ruier:ruie					CondEnd ;
ruier:ruie					GerundEnd ;
ruier						InfEnd ;
ruier+Verb+Impv+2P+Sg:rui	# ;
ruier+Verb+Impv+2P+Pl:ruiai	# ;


LEXICON SAVAIR
savair+Verb+PresInd+1P+Sg:sai		PronSg1 ;
savair+Verb+PresInd+2P+Sg:sas		# ;
savair+Verb+PresInd+3P+Sg:sa		PronSg3Short ;
savair+Verb+PresInd+1P+Pl:savain	PronPl1 ;
savair+Verb+PresInd+2P+Pl:savais	# ;
savair+Verb+PresInd+3P+Pl:san		PronPl3 ;
savair:save						ImpIndEnd ;
savair:sappi					ConjEnd ;
savair:save						CondEnd ;
savair:save						GerundEnd ;
savair							InfEnd ;
savair+Verb+Impv+2P+Sg:sappias	# ;
savair+Verb+Impv+2P+Pl:sappias	# ;


LEXICON STAR
star+Verb+PresInd+1P+Sg:stun	PronSg1 ;
star+Verb+PresInd+2P+Sg:stas	# ;
star+Verb+PresInd+3P+Sg:statt	PronSg3Cons ;
star+Verb+PresInd+1P+Pl:stain	PronPl1 ;
star+Verb+PresInd+2P+Pl:stais	# ;
star+Verb+PresInd+3P+Pl:stattan	PronPl3 ;
star:ste					ImpIndEnd ;
star:stetti					ConjEnd ;
star:ste					CondEnd ;
star:sto					GerundEnd ;
star						InfEnd ;
star+Verb+Impv+2P+Sg:stà	# ;
star+Verb+Impv+2P+Pl:stai	# ;


LEXICON STUAIR
stuair+Verb+PresInd+1P+Sg:stoss		PronSg1 ;
stuair+Verb+PresInd+2P+Sg:stos		# ;
stuair+Verb+PresInd+3P+Sg:sto		PronSg3Short ;
stuair+Verb+PresInd+1P+Pl:stuain	PronPl1 ;
stuair+Verb+PresInd+2P+Pl:stuais	# ;
stuair+Verb+PresInd+3P+Pl:ston		PronPl3 ;
stuair:stue						ImpIndEnd ;
stuair:stoppi					ConjEnd ;
stuair:stue						CondEnd ;
stuair:stue						GerundEnd ;
stuair							InfEnd ;


LEXICON SURRIR
surrir+Verb+PresInd+1P+Sg:surri		PronSg1 ;
surrir+Verb+PresInd+2P+Sg:surris		# ;
surrir+Verb+PresInd+3P+Sg:surri		PronSg3Short ;
surrir+Verb+PresInd+1P+Pl:surriain	PronPl1 ;
surrir+Verb+PresInd+2P+Pl:surriais	# ;
surrir+Verb+PresInd+3P+Pl:surrin		PronPl3 ;
surrir:surrie						ImpIndEnd ;
surrir:surri					ConjEnd ;
surrir:surrie					CondEnd ;
surrir:surrie					GerundEnd ;
surrir							InfEnd ;
surrir+Verb+Impv+2P+Sg:surri		# ;
surrir+Verb+Impv+2P+Pl:sirriai	# ;


LEXICON TERRENISAR
terrenisar+Verb+PresInd+3P+Sg:terrena	# ;
terrenisar+Verb+ImpInd+3P+Sg:terrenava	# ;
terrenisar+Verb+Con+3P+Sg:terrenia		# ;
terrenisar+Verb+Cond+3P+Sg:terrenass	# ;
terrenisar+Verb+Gerund:terrenond		# ;
terrenisar						InfEnd ;
! suffigated forms:
terrenisar+Verb+PresInd+3P+Sg^|+Pron+Pers+Nom+1P+MF+Sg:terreni	# ;
terrenisar+Verb+ImpInd+3P+Sg^|+Pron+Pers+Nom+1P+MF+Sg:terrenavi	# ;
terrenisar+Verb+Cond+3P+Sg^|+Pron+Pers+Nom+1P+MF+Sg:terrenassi	# ;


LEXICON VAIR
vair+Verb+PresInd+1P+Sg:ves		PronSg1 ;
vair+Verb+PresInd+2P+Sg:vesas		# ;
vair+Verb+PresInd+3P+Sg:vesa		PronSg3Short ;
vair+Verb+PresInd+1P+Pl:vesain	PronPl1 ;
vair+Verb+PresInd+2P+Pl:vesais	# ;
vair+Verb+PresInd+3P+Pl:vesan		PronPl3 ;
vair:vese					ImpIndEnd ;
vair:vesi					ConjEnd ;
vair:vese					CondEnd ;
vair:vese					GerundEnd ;
vair						InfEnd ;


LEXICON VEGNIR
vegnir+Verb+PresInd+1P+Sg:vegn		PronSg1 ;
vegnir+Verb+PresInd+2P+Sg:vegns		# ;
vegnir+Verb+PresInd+3P+Sg:vegn		PronSg3Cons ;
vegnir+Verb+PresInd+1P+Pl:vegnin	PronPl1 ;
vegnir+Verb+PresInd+2P+Pl:vegnis	# ;
vegnir+Verb+PresInd+3P+Pl:vegnan	PronPl3 ;
vegnir:vegni					ImpIndEnd ;
vegnir:vegni					ConjEnd ;
vegnir:vegni					CondEnd ;
vegnir:vegni					GerundEnd ;
vegnir							InfEnd ;
vegnir+Verb+Impv+2P+Sg:ve		# ;
vegnir+Verb+Impv+2P+Pl:vegni	# ;


LEXICON VULAIR
vulair+Verb+PresInd+1P+Sg:vi		PronSg1 ;
vulair+Verb+PresInd+2P+Sg:vuls		# ;
vulair+Verb+PresInd+3P+Sg:vul		PronSg3Cons ;
vulair+Verb+PresInd+1P+Pl:vulain	PronPl1 ;
vulair+Verb+PresInd+2P+Pl:vulais	# ;
vulair+Verb+PresInd+3P+Pl:vulan		PronPl3 ;
vulair:vule						ImpIndEnd ;
vulair:vegli					ConjEnd ;
vulair:vule						CondEnd ;
vulair:vule						GerundEnd ;
vulair							InfEnd ;
vulair+Verb+Impv+2P+Sg:veglias	# ;
vulair+Verb+Impv+2P+Pl:veglias	# ;
! Short Forms:
vulair+Verb+PresInd+1P+Pl:lain	PronPl1 ;
vulair+Verb+ImpInd+1P+Sg:leva	# ;
vulair+Verb+Cond+1P+Sg:less		PronSg1 ;

! Prefixes:
!==========
LEXICON CUNTRAPREFIX
cuntra	FAR ;

LEXICON DISPREFIX
dis		FAR ;

LEXICON PREPREFIX
pre		VAIR ;
pre		VEGNIR ;

LEXICON REPREFIX
re		VAIR ;

LEXICON SPREFIX
s		FAR ;
s		FUGIR ;
s		NUAR ;

LEXICON STRAPREFIX
stra	COIER  ;

LEXICON SUTTAPREFIX
sutta	STAR ;

! Endings:
!=========
! Imperfect Indicative:
LEXICON ImpIndEnd
+Verb+ImpInd+1P+Sg:va	# ;
+Verb+ImpInd+2P+Sg:vas	# ;
+Verb+ImpInd+3P+Sg:va	# ;
+Verb+ImpInd+1P+Pl:van	# ;
+Verb+ImpInd+2P+Pl:vas	# ;
+Verb+ImpInd+3P+Pl:van	# ;
!! Suffigated Forms:
+Verb+ImpInd+3P+Sg+Apo:v'	# ;
+Verb+ImpInd+3P+Sg^|+Pron+Pers+Nom+3P+Masc+Sg:va'l	# ;
+Verb+ImpInd+3P+Sg^|+Pron+Pers+Nom+3P+Fem+Sg:v'la	# ;
+Verb+ImpInd+3P+Sg^|+Pron+Pers+Nom+3P+MF+Sg:vi		# ;
+Verb+ImpInd+1P+Pl^|+Pron+Pers+Nom+1P+MF+Pl:vansa	# ;
+Verb+ImpInd+3P+Pl^|+Pron+Pers+Nom+3P+MF+Pl:vani		# ;


! Conjunctive:
LEXICON ConjEnd
+Verb+Con+1P+Sg:a	# ;
+Verb+Con+2P+Sg:as	# ;
+Verb+Con+3P+Sg:a	# ;
+Verb+Con+1P+Pl:an	# ;
+Verb+Con+2P+Pl:as	# ;
+Verb+Con+3P+Pl:an	# ;
!! Suffigated Forms:
+Verb+Con+3P+Sg+Apo:an	# ;
+Verb+Con+1P+Pl^|+Pron+Pers+Nom+1P+MF+Pl:ansa	#;
+Verb+Con+3P+Pl^|+Pron+Pers+Nom+3P+MF+Pl:ani	#;


! Conditional
LEXICON CondEnd
+Verb+Cond+1P+Sg:ss		# ;
+Verb+Cond+2P+Sg:ssas	# ;
+Verb+Cond+3P+Sg:ss		# ;
+Verb+Cond+1P+Pl:ssan	# ;
+Verb+Cond+2P+Pl:ssas	# ;
+Verb+Cond+3P+Pl:ssan	# ;
!! Suffigated Forms:
+Verb+Cond+1P+Sg^|+Pron+Pers+Nom+1P+MF+Sg:ssa	#;
+Verb+Cond+3P+Sg^|+Pron+Pers+Nom+3P+MF+Sg:ssi	#;
+Verb+Cond+1P+Pl^|+Pron+Pers+Nom+1P+MF+Pl:ssansa	#;
+Verb+Cond+3P+Pl^|+Pron+Pers+Nom+3P+MF+Pl:ssani	#;


! Gerund
LEXICON GerundEnd
+Verb+Gerund:nd	# ;


! Infinitive
LEXICON InfEnd
+Verb+Inf:0	# ;



! Suffigated Pronouns:
!=====================
LEXICON PronSg1
^|+Pron+Pers+Nom+1P+MF+Sg:a	#;
#;

LEXICON PronSg3
^|+Pron+Pers+Nom+1P+Masc+Sg:a'l	#;
^|+Pron+Pers+Nom+1P+Fem+Sg:'la	#;
^|+Pron+Pers+Nom+1P+MF+Sg:i		#;
+Apo:'	#;
#;

LEXICON PronSg3Short
^|+Pron+Pers+Nom+1P+Masc+Sg:'l	#;
^|+Pron+Pers+Nom+1P+Fem+Sg:'la	#;
^|+Pron+Pers+Nom+1P+MF+Sg:*DOUBi		#;
+Apo:n	#;
#;

LEXICON PronSg3Cons
^|+Pron+Pers+Nom+1P+MF+Sg:i	#;
#;

LEXICON PronPl1
^|+Pron+Pers+Nom+1P+MF+Pl:sa	#;
#;

LEXICON PronPl3
^|+Pron+Pers+Nom+3P+MF+Pl:i	#;
#;